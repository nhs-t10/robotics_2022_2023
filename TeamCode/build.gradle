//
// build.gradle in TeamCode
//
// Most of the definitions for building your module reside in a common, shared
// file 'build.common.gradle'. Being factored in this way makes it easier to
// integrate updates to the FTC into your code. If you really need to customize
// the build definitions, you can place those customizations in this file, but
// please think carefully as to whether such customizations are really necessary
// before doing so.


// Custom definitions may go here
plugins {
    id "com.github.node-gradle.node" version "3.0.1"
}


// Include common definitions from above.
apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'

dependencies {
    implementation project(':FtcRobotController')
    annotationProcessor files('lib/OpModeAnnotationProcessor.jar')
    implementation 'junit:junit:4.13.2'
    testImplementation 'junit:junit:4.13.2'
    implementation 'org.jetbrains:annotations-java5:23.0.0'
}


task autoautoCompiler(type: NodeTask) {
    args = ["--build-history", "--agpbi", "--make-tests", "--run-cleanup",
            //The `--in` and `--out` arguments use GStrings, a fancy way of putting strings together.
            //See <http://docs.groovy-lang.org/latest/html/documentation/index.html#all-strings> for more info.

            "--in=${file("src/main/java")}", "--in=${file("src/test/java")}", //Directories to take Autoauto files from
            "--out=${file("gen")}", //directory to put generated opmodes into
            "--test-dir=${file("src/test/java")}", //directory to toss tests into
            "--assets-dir=${file("src/main/assets")}" //directory to put build-history into
    ]
    script = file("src/main/scripts/autoauto-compiler/index.js")
}

android {
    androidResources {
        noCompress 'tflite'
    }
    gradle.projectsEvaluated {
        preBuild.dependsOn(autoautoCompiler)
    }

    sourceSets {
        test {
            java.srcDir("gen/")
            java.srcDir("src/test/java")
        }
        debug {
            java.srcDir("gen/")
        }
        main {
            java.srcDir("gen/")

        }
    }
    defaultConfig {
        minSdkVersion 23
    }
}

node {
    download = true
    version = "12.18.3"
    distBaseUrl = "https://nodejs.org/dist"
}